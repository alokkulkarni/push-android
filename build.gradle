// Top-level build file where you can add configuration options common to all sub-projects/modules.

////////////////////////////////////////////////////////////////////////////////////////////////////
// Ensure your ~/.gradle/gradle.properties file or environment defines the following properties:
// MAVEN_USER = your user name on the above Artifactory.
// MAVEN_PASS = your password on the above Artifactory.
////////////////////////////////////////////////////////////////////////////////////////////////////

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

ext {
    pushSdkVersionCode = 8
    pushSdkVersionName = System.getenv("PUSH_SDK_VERSION") ?: "1.6.1"
    isSnapshot = false
}

subprojects {

    apply plugin: 'maven'

    group = "io.pivotal.android"

    if (isSnapshot) {
        version =  pushSdkVersionName + "-SNAPSHOT"
    } else {
        version =  pushSdkVersionName
    }

    repositories {
        mavenCentral()
    }

    apply plugin: 'android-library'

    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.2"

        defaultConfig {
            versionCode pushSdkVersionCode
            versionName pushSdkVersionName
            minSdkVersion 16
            targetSdkVersion 23
        }
    }

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {

            pom.project {

                name 'Push client SDK for Android'
                packaging 'aar'
                description 'A comprehensive SDK for receiving and sending Push Notification through Pivotal Cloud Foundry\'s Push API'
                url 'https://github.com/cfmobile/push-android'

                licenses {
                    license {
                        // TODO - provide a more correct license file and URL
                        name 'Pivotal Software License'
                        url 'https://github.com/cfmobile/push-android/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                scm {
                    url 'https://github.com/cfmobile/push-android'
                    connection 'scm:git:git@github.com/cfmobile/push-android.git'
                    developerConnection 'scm:git:ssh://github.com:cfmobile/push-android.git'
                }

                developers {
                    developer {
                        id 'cf-push-notifications'
                        name 'PCF Push Nofitications'
                        email 'cf-push-notifications@poivotal.io'
                    }
                }
            }

            // Publishes to local repository
            repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
       }
    }

    //Creating sources with comments
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    //Put the androidSources to the artifacts
    artifacts {
        archives androidSourcesJar
    }

    task(printVersion) << {
        println "My version is: " + pushSdkVersionName
    }
}
