// Top-level build file where you can add configuration options common to all sub-projects/modules.

////////////////////////////////////////////////////////////////////////////////////////////////////
// Ensure your ~/.gradle/gradle.properties file or environment defines the following properties:
// MAVEN_USER = your user name on the above Artifactory.
// MAVEN_PASS = your password on the above Artifactory.
////////////////////////////////////////////////////////////////////////////////////////////////////

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

ext {
    pushSdkVersionCode = 8
    pushSdkVersionName = System.getenv("PUSH_SDK_VERSION") ?: "1.7.0"
    isSnapshot = false
}

subprojects {

    apply plugin: 'maven'

    group = "io.pivotal.android"

    if (isSnapshot) {
        version =  pushSdkVersionName + "-SNAPSHOT"
    } else {
        version =  pushSdkVersionName
    }

    repositories {
        mavenCentral()
    }

    apply plugin: 'android-library'

    android {
        compileSdkVersion 24
        buildToolsVersion "24.0.2"

        defaultConfig {
            versionCode pushSdkVersionCode
            versionName pushSdkVersionName
            minSdkVersion 16
            targetSdkVersion 24
        }
    }

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {

            pom.project {
                licenses {
                    license {
                        // TODO - provide a more correct license file and URL
                        name 'Pivotal Software License'
                        url 'https://github.com/cfmobile/push-android/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }
            }

            // Publishes to local repository
            repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
       }
    }

    task(printVersion) << {
        println "My version is: " + pushSdkVersionName
    }
}
